apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
  maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
  mavenCentral()
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

archivesBaseName = "mock"

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
  compile project(":api")

  testCompile "junit:junit:$junit_version"
  testCompile "com.google.truth:truth:$truth_version"
  testCompile "org.mockito:mockito-core:$mockito_core_version"
  testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Russound Matrix Mock Receiver'
        packaging 'jar'
        groupId = release_groupId
        version = release_version
        artifactId 'mock'
        description 'An API that lets create a mock receiver to test your instance against.'
        url 'https://github.com/holmes/russound'

        scm {
          connection 'scm:git:git://github.com/holmes/russound.git'
          developerConnection 'scm:git:ssh://github.com:holmes/russound.git'
          url 'https://github.com/holmes/russound/tree/master'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'holmes'
            name 'Jason Holmes'
            email 'holmes.j@gmail.com'
          }
        }
      }
    }
  }
}
